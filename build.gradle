plugins {
    id 'application'
    id 'org.springframework.boot' version '2.7.4'
    id 'io.spring.dependency-management' version '1.0.14.RELEASE'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'java'
    id 'jacoco'
}

group = 'com.between'
version = '0.0.1-SNAPSHOT'
mainClassName = 'com.between.Program'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.springfox:springfox-boot-starter:3.0.0'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.elasticsearch:elasticsearch:7.10.2'
    implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.10.2'
    runtimeOnly 'mysql:mysql-connector-java'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-core:4.8.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.amqp:spring-rabbit-test'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jar {
    enabled = false
}

jacoco {
    toolVersion = "0.8.8"
    reportsDirectory = file("$buildDir/reports/jacoco")
}

jacocoTestReport { // ./gradlew test jacocoTestReport from CLI
    dependsOn test // tests are required to run before generating the report
    group = "Reporting"
    reports {
        xml.required.set(true)
        csv.required.set(true)
        html.destination file("${buildDir}/reports/coverage")
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/between/Program.*',
            ])
        }))
    }
}
